
      SUBROUTINE TEST7
C-----------------------------------------------------------------
C     EXAMPLE: THE SCREENING OF CARBON IN AN HYDROGEN PLASMA: 
C     THE TRANSITION FROM THE COLLISIONAL TO THE COLLISIONLESS
C     CASE. 
C-----------------------------------------------------------------
      IMPLICIT NONE

      INTEGER NS,NC,NAR,ISEL,NREG,NLEG,NENERGY,NCOF,
     +        IC,NZM,I,J,NMAXGR,L,ISHOT
      REAL M,T,DEN,DS,CFF4,XI,TAU,EPS, SIGMA, NORM, ZSP, EPARR
      REAL RHO, RN, E, Q, BN
      LOGICAL NEOGEO, NEOFRC

      include 'elem_config.inc'
      
      parameter(NAR = NELMAX+2)
      parameter(NZM = NIONMAX)
      PARAMETER(NMAXGR = 1000)


      DIMENSION NC(NAR),ZSP(NAR,NZM),M(NAR),T(NAR),DEN(NAR,NZM),
     +          DS(NAR,NZM,2),CFF4(NAR,NZM,4),XI(NAR,NZM),
     +          TAU(NAR,NAR),SIGMA(4)


C     SET THE PARAMETERS FOR THE CIRCULAR GEOMETRY
      RHO = 0.2
      E = 1e-1
      Q = 2
      RN = 1.65
      BN = 2.5
C     COPY THEM INTO THE VALUES USED BY THE CODE
      CALL CIRCGEOM(1,RHO,RN,E,Q,BN)
C     USE THE CIRCULAR GEOMETRY APPROXIMATION
      ISEL = 2
C     SET THE ACCURACY
      EPS = 1E-5
c     FORCE THE BANANA REGIME IN THE CALCULATION OF THE TRANSPORT
C     FLUXES (IN THE CALCULATION OF THE VISCOSITY)
      NREG = 1
c     THE NUMBER OF LEGENDRE HARMONICS
      NLEG = 3
C     USE ENERGY SCATTERING IN THE CALC. OF VISCOSITY
      NENERGY = 1
C     ION-ELECTRON COLLISIONS
      NCOF = 1
C     IN THE FIRST CALL THE MATRICES HAVE TO BE CALCULATED
      NEOFRC = .FALSE.
C     RECALCULATE THE GEOMETRY 
      NEOGEO = .TRUE. 
C     NO SHOT NUMBER 
      ISHOT = 0 
C     ZERO PARALLEL ELECTRIC FIELD 
      EPARR = 0. 
C     CALCULATE THE PFIRSCH SCHLUETER CONTRIBUTION
      IC = 2
C     SET THE SIGMAS
      SIGMA(1) = 0.
      SIGMA(2) = 0.
      SIGMA(3) = 1.
      SIGMA(4) = 1.

C     THE NUMBER OF SPECIES IS 2
      NS = 2
C     IONS AND ELECTRONS HAVE ONLY ONE CHARGE 
      NC(1) = 1
      NC(2) = 1
C     THE MASS OF THE HYDROGEN AND CARBON
      M(1) = 1.6727E-27
      M(2) = 12*1.6727E-27
C     THE CHARGE OF THE HYDROGEN AND CARBON
      ZSP(1,1) = 1
      ZSP(2,1) = 6
C     THE DENSITY OF THE SPECIES IN 10^19 M^-3
      DEN(1,1) = 5.
      DEN(2,1) = DEN(1,1)/ZSP(2,1)**2 
C     THE TEMPERATURE IN KEV

      DO 1111 L = 1, 100

      T(1) = 10.*EXP(-3.*(L-1.)/99.*LOG(10.))
      T(2) = T(1)

      DO 204 I = 1, NS
        DO 204 J = 1, NC(I)
          DS(I,J,1) = 0.
          DS(I,J,2) = 0.
 204  CONTINUE
      DS(2,1,2) = 1.
      DS(2,1,1) = 1.
      DS(1,1,1) = 1.
      DS(1,1,2) = 1.


      CALL NEOART(NS,NC,NAR,NZM,ZSP,M,T,DEN,DS,RHO,EPS,
     +            ISEL,ISHOT,NREG,SIGMA,NLEG,NENERGY,NCOF,
     +            NEOGEO,NEOFRC,IC,EPARR,CFF4)


      CALL COLXI(NAR,NZM,NS,NC,ZSP,DEN,T,M,TAU,XI)

      NORM = ZSP(2,1)*1.6E-22*BN**2/(2*(Q)**2
     +       *T(2)*TAU(1,2))

      WRITE(*,1000)T(2),CFF4(2,1,1)*NORM

 1000 FORMAT(2(1X,1PE13.5))

 1111 CONTINUE

      RETURN 
      END
