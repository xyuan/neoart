
      SUBROUTINE TEST12
C-----------------------------------------------------------------
C     CALCULATE THE EPSILON (INVERSE ASPECT RATIO) DEPENDENCE 
C     OF THE ION HEAT FLUX IN A PURE PLASMA
C-----------------------------------------------------------------
      IMPLICIT NONE

      INTEGER NS,NC,NAR,ISEL,NREG,NLEG,NENERGY,NCOF,
     +        IC,NZM,I,J,NMAXGR,L,NMG,mmx
      REAL*8 M,T,DEN,DS,CFF4,XI,TAU, EPS, SIGMA, NORM, RESUL
      REAL*8 RHO, RN, E, Q, BN, BAV, B2AV, BI2A, RBT, BGRADP,
     +       DPSIDR, RNQ, FC, FM, ZSP
      LOGICAL NEOGEO, NEOFRC

      PARAMETER(NAR = 5)
      PARAMETER(NZM = 30)
      PARAMETER(NMAXGR = 1000)

      DIMENSION NC(NAR),ZSP(NAR,NZM),M(NAR),T(NAR),DEN(NAR,NZM),
     +          DS(NAR,NZM,2),CFF4(NAR,NZM,4),XI(NAR,NZM),
     +          TAU(NAR,NAR),SIGMA(4),RESUL(22,9),FM(NMAXGR)


C     USE THE CIRCULAR GEOMETRY APPROXIMATION
      ISEL = 2
C     SET THE ACCURACY
      EPS = 1E-5
c     FORCE THE BANANA REGIME IN THE CALCULATION OF THE TRANSPORT
C     FLUXES (IN THE CALCULATION OF THE VISCOSITY)
      NREG = 1
c     THE NUMBER OF LEGENDRE HARMONICS
      NLEG = 2
C     USE ENERGY SCATTERING IN THE CALC. OF VISCOSITY
      NENERGY = 1
C     SWITCH OFF ION-ELECTRON COLLISIONS
      NCOF = 0
C     IN THE FIRST CALL THE MATRICES HAVE TO BE CALCULATED
      NEOFRC = .FALSE.
C     RECALCULATE THE GEOMETRY DEPENDENT PARAMETERS EVERY TIME
      NEOGEO = .TRUE.

C     THE NUMBER OF SPECIES IS 2
      NS = 2
C     IONS AND ELECTRONS HAVE ONLY ONE CHARGE 
      NC(1) = 1
      NC(2) = 1
C     THE MASS OF THE ELECTRON AND PROTON
      M(1) = 9.1096E-31
      M(2) = 1.6727E-27
C     THE CHARGE OF THE ELECTRON AND ION
      ZSP(1,1) = -1
      ZSP(2,1) = 1
C     THE DENSITY OF THE SPECIES IN 10^19 M^-3
      DEN(1,1) = 1.
      DEN(2,1) = 1. 
C     THE TEMPERATURE IN KEV
      T(1) = 10.
      T(2) = 10.

C     LOOP OVER INVERSE ASPECT RATIO
      DO 10000 L = 1, 22

C     SET THE PARAMETERS FOR THE CIRCULAR GEOMETRY
      RHO = 0.05
      E = 1E-4 + 0.9899*(L-2.)/20.
      IF (L.EQ.1) E = 1E-4
      IF (L.EQ.2) E = 1E-2
      Q = 2
      RN = 1.65
      BN = 2.5
C     COPY THEM INTO THE VALUES USED BY THE CODE
      CALL CIRCGEOM(1,RHO,RN,E,Q,BN)


C     CALCULATE THE BANANA PLATEAU CONTRIBUTION
      IC = 1

      DO 304 I = 1, NS
        DO 304 J = 1, NC(I)
          DS(I,J,1) = 0.
          DS(I,J,2) = 0.
 304  CONTINUE
      DS(2,1,2) = 1.


      CALL NEOART(NS,NC,NAR,NZM,ZSP,M,T,DEN,DS,RHO,EPS,
     +            ISEL,NREG,SIGMA,NLEG,NENERGY,NCOF,
     +            NEOGEO,NEOFRC,IC,CFF4)


      CALL COLXI(NAR,NZM,NS,NC,ZSP,DEN,T,M,TAU,XI)

      NORM = 1.6E-22*BN**2*E**2/(2*(Q)**2*(1.-E**2)
     +       *T(2)*TAU(2,2))

C     USE INVERSE ASPECT RATIO AS X AXIS
      RESUL(L,1) = E
      RESUL(L,2) = -SQRT(2.)*CFF4(2,1,2)*NORM
      RESUL(L,3) = 0.68*SQRT(E)

      NMG = NMAXGR
      CALL GEOM(NMG,ISEL,RHO,EPS,BAV,B2AV,BI2A,RBT,BGRADP,
     +          DPSIDR,RNQ,FC,FM,MMX)

      RESUL(L,4) = 1.-FC

C     CALCULATE THE PFIRSCH SCHLUETER FLUXES
      IC = 2
C     SET THE COUPLING
      SIGMA(1) = 0
      SIGMA(2) = 0
      SIGMA(3) = 0 
      SIGMA(4) = 0

C     THE THERMODYNAMIC FORCES
      DO 204 I = 1, NS
        DO 204 J = 1, NC(I)
          DS(I,J,1) = 0.
          DS(I,J,2) = 0.
 204  CONTINUE
      DS(2,1,2) = 1.


      CALL NEOART(NS,NC,NAR,NZM,ZSP,M,T,DEN,DS,RHO,EPS,
     +            ISEL,NREG,SIGMA,NLEG,NENERGY,NCOF,
     +            NEOGEO,NEOFRC,IC,CFF4)

      RESUL(L,5) = -SQRT(2.)*CFF4(2,1,2)*NORM

C     CALCULATE THE SUM (JUST FOR TESTING)
      IC = 3

C     THE THERMODYNAMIC FORCES
      DO 404 I = 1, NS
        DO 404 J = 1, NC(I)
          DS(I,J,1) = 0.
          DS(I,J,2) = 0.
 404  CONTINUE
      DS(2,1,2) = 1.


      CALL NEOART(NS,NC,NAR,NZM,ZSP,M,T,DEN,DS,RHO,EPS,
     +            ISEL,NREG,SIGMA,NLEG,NENERGY,NCOF,
     +            NEOGEO,NEOFRC,IC,CFF4)

      RESUL(L,6) = -SQRT(2.)*CFF4(2,1,2)*NORM

C     CALCULATE THE ANALYTIC RESULTS
      RESUL(L,7) = SQRT(1.-E**2)*0.46*(1.-FC)/(FC + 0.46*(1.-FC))
      RESUL(L,8) = 1 + 1.5*E**2 - SQRT(1.-E**2)
      RESUL(L,9) = RESUL(L,7) + RESUL(L,8)

10000 CONTINUE

      OPEN(11, FILE = 'TEST12.DAT')
C     THE COLLUMS HAVE THE FOLLOWING MEANING
C     COLLUM  1. : THE INVERSE ASPECT RATIO
C     COLLUM  2. : THE CODE RESULT FOR THE BANANA PLATEAU ION
C                  HEAT FLUX
C     COLLUM  3. : THE LOWEST ORDER (IN EPSILON) RESULT (ANAL)
C     COLLUM  4. : THE NUMBER OF TRAPPED PARTICLES
C     COLLUM  5. : THE CODE RESULT FOR THE PFIRSCH SCHLUETER 
C                  ION HEAT FLUX
C     COLLUM  6. : THE CODE RESULT OF THE TOTAL ION HEAT FLUX
C     COLLUM  7. : THE ANALYTIC RESULT OF THE BANANA PLATEAU
C                  CONTRIBUTION. 
C     COLLUM  8. : THE ANALYTIC RESULT FOR THE PFRISCH SCHLUETER
C                  FLUX
C     COLLUM  9. : ANALYTIC RESULT FOR THE TOTAL FLUX
      DO 30000 L = 1, 22
        WRITE(11,30001)(RESUL(L,I),I = 1,9)
30000 CONTINUE
30001 FORMAT(9(1X,1PE13.5))

      RETURN
      END
