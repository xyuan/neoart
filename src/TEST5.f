
      SUBROUTINE TEST5
C-----------------------------------------------------------------
C     CALCULATE THE HEAT TRANSPORT OF A PLASMA CONTAINING
C     OF HYDROGEN AND OXYGEN, AND COMPARE THEM WITH THE ANALYTIC 
C     EXPRESSIONS OF HIRSHMANN AND SIGMAR, AND THE DERIVED 
C     EXPRESSION KEEPING FINITE  MASS RATIO EFFECTS.
C-----------------------------------------------------------------
      IMPLICIT NONE

      INTEGER NS,NC,NAR,ISEL,NREG,NLEG,NENERGY,NCOF,
     +        IC,NZM,I,J,NMAXGR,L,ISHOT
      REAL*8 M,T,DEN,DS,CFF4,XI,TAU,C3,ZSP,
     +       EPS, SIGMA, NORM, ALPHA, OMTH, OMTI, RESUL, EPARR
      REAL*8 RHO, RN, E, Q, BN
      LOGICAL NEOGEO, NEOFRC

      include 'elem_config.inc'
      
      parameter(NAR = NELMAX+2)
      parameter(NZM = NIONMAX)
      PARAMETER(NMAXGR = 1000)

      DIMENSION NC(NAR),ZSP(NAR,NZM),M(NAR),T(NAR),DEN(NAR,NZM),
     +          DS(NAR,NZM,2),CFF4(NAR,NZM,4),XI(NAR,NZM),
     +          TAU(NAR,NAR),SIGMA(4),RESUL(20,10)


C     SET THE PARAMETERS FOR THE CIRCULAR GEOMETRY
      RHO = 0.05
      E = 1E-4
      Q = 2
      RN = 1.65
      BN = 2.5
C     COPY THEM INTO THE VALUES USED BY THE CODE
      CALL CIRCGEOM(1,RHO,RN,E,Q,BN)
C     USE THE CIRCULAR GEOMETRY APPROXIMATION
      ISEL = 2
C     SET THE ACCURACY
      EPS = 1E-5
C     ION-ELECTRON COLLISIONS
      NCOF = 1
C     IN THE FIRST CALL THE MATRICES HAVE TO BE CALCULATED
      NEOFRC = .FALSE.
C     RECALCULATE THE GEOMETRY 
      NEOGEO = .TRUE. 
C     NOT SHOT NUMBER 
      ISHOT = 0 
C     ZERO ELECTRIC FIELD 
      EPARR = 0. 
C     CALCULATE THE PS CONTRIBUTION
      IC = 2
C     SET THE COUPLING (NO COUPLING)
      SIGMA(1) = 0 
      SIGMA(2) = 0
      SIGMA(3) = 0
      SIGMA(4) = 0
C     USE THE COLLISIONAL LIMIT
      T(1) = 5.
      T(2) = 5.

C     THE NUMBER OF SPECIES IS 2
      NS = 2
C     IONS HAVE ONLY ONE CHARGE STATE
      NC(1) = 1
      NC(2) = 1
C     THE MASS OF THE HYDROGEN AND OXYGEN
      M(1) = 1.6727E-27
      M(2) = 16*1.6727E-27
C     THE CHARGE OF THE HYDROGEN AND OXYGEN
      ZSP(1,1) = 1
      ZSP(2,1) = 4
C     THE DENSITY OF THE SPECIES IN 10^19 M^-3
      DEN(1,1) = 1.

C     LOOP OVER ALPHA
      DO 20000 L = 1, 20
        
      DEN(2,1) = 1E-1*EXP(3*(L-1)*LOG(10.)/19)/ZSP(2,1)**2

      NEOFRC = .FALSE.
C     THE THERMODYNAMIC FORCES
      DO 204 I = 1, NS
        DO 204 J = 1, NC(I)
          DS(I,J,1) = 0.
          DS(I,J,2) = 0.
 204  CONTINUE
      DS(2,1,2) = 1.


      CALL NEOART(NS,NC,NAR,NZM,ZSP,M,T,DEN,DS,RHO,EPS,
     +            ISEL,ISHOT,NREG,SIGMA,NLEG,NENERGY,NCOF,
     +            NEOGEO,NEOFRC,IC,EPARR,CFF4)


      CALL COLXI(NAR,NZM,NS,NC,ZSP,DEN,T,M,TAU,XI)


      NORM = 1.6E-22*BN**2/(2.*Q**2*T(1))
      ALPHA  = DEN(2,1)*ZSP(2,1)**2/(DEN(1,1)*ZSP(1,1)**2)


      OMTH = SQRT(2*1.6E-16*T(1)*M(1))*DEN(1,1)*1E19/TAU(1,1)
     +       /(Q*RN)
      OMTI = SQRT(2*1.6E-16*T(2)*M(2))*DEN(2,1)*1E19/TAU(2,2)
     +       /(Q*RN)

C     USE ALPHA AS X COORDINATE
      RESUL(L,1) = ALPHA

C     CODE RESULT
      RESUL(L,2) = -ZSP(2,1)**2*CFF4(2,1,2)*NORM/TAU(2,2)

C     RESULT OF HIRSHMANN SIGMAR
      RESUL(L,3) = C3(0.D0,OMTI)

C     RESULT WITH FINITE MASS RATIO
      RESUL(L,4) = - 27*ZSP(2,1)/(4.*ALPHA) * SQRT(M(1)/M(2))**3
     +               + 15./(2.*ALPHA)*SQRT(M(1)/M(2)) + SQRT(2.)

20000 CONTINUE

C     OPEN(11,FILE = 'TEST5.DAT')
C     THE COLLUMS HAVE THE FOLLOWING MEANING
C     COLLUM  1. : ALPHA
C     COLLUM  2. : CODE RESULT 
C     COLLUM  3. : ANANLYTIC FUNCTION C3(0,OMTI)
C     COLLUM  4. : FINITE MASS RATIO RESULT (ANALYTIC)
      DO 30000 I = 1, 20
        WRITE(*,1000)(RESUL(I,L),L=1,4)
30000 CONTINUE

 1000 FORMAT(14(1X,1PE13.5))

      RETURN 
      END
